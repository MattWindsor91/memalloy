(*
 * The ARMv8 Application Level Memory Model.
 * Copyright (C) 2016, ARM Ltd.
 *)

"ARM8"

include "basic_H.cat"
include "ftxn.cat" (* subsumes fr.cat *)

(* Coherence-after *)
let ca = fr | co

(* Observed-by *)
let obs = rfe | fre | coe

(* Dependency-ordered-before *)
let dob = addr | data
    | ctrl; [W]
    | ((ctrl & isb) | (addr; isb)); [R]
    | addr; po; [W]
    | (ctrl | data); coi
    | (addr | data); rfi

(* Atomic-ordered-before *)
let aob = atom
    | atom; rfi

(* Barrier-ordered-before *)
let bob = dmb
    | [SCREL]; po; [SCACQ]
    | [R]; dmbld
    | [SCACQ]; po
    | [W]; dmbst; [W]
    | po; [SCREL]
    | po; [SCREL]; coi
    | po & into(stxn)
    | po & outof(stxn)

(* Ordered-before *)
let ob0 = obs
    | dob
    | aob
    | bob
let ob = ob0+

(* Internal visibility requirement *)
acyclic po-loc | ca | rf as internal

(* External visibility requirement *)
irreflexive ob as external

(* Atomic: Basic LDXR/STXR constraint to forbid intervening writes. *)
empty atom & (fre; coe) as AtomicRMW

let com = rf | fr | co | co;rf
acyclic stronglift(com, stxn) as StrongIsolation
acyclic stronglift(ob, stxn) as TxnOrder
empty ftxn & ((fre|coe); rfe) as AtomicFtxn
