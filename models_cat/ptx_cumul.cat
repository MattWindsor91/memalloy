(* Cumulative version of the PTX model. From Section 6.2 of:
https://johnwickerson.github.io/papers/memalloy.pdf#page=10
 *)

"PTX"

include "basic.cat"

empty atom & (fre;coe) as Atomic
				      
let polocLLH = poloc & ((W*M) | (M*W))
acyclic polocLLH | com as ScPerLocLLH

let dp = addr | data | ctrl
acyclic dp | rf as NoThinAir

let sys_fence = po; [membar.sys]; po
let gl_fence = (po; [membar.gl]; po) | sys_fence
let cta_fence = (po; [membar.cta]; po) | gl_fence

let rmo(r,f) = dp | rfe | co | fr | (r?; f; r?)

let CTArmo = rmo([_], cta_fence) & scta
let GLrmo = rmo(CTArmo+, gl_fence) & sgl
let SYSrmo = rmo(GLrmo+, sys_fence)

acyclic CTArmo as CTAconstraint
acyclic GLrmo as GLconstraint
acyclic SYSrmo as SYSconstraint


