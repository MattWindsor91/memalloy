(* This is a modified version of a file distributed with the paper
"Overhauling SC atomics in C11 and OpenCL" by Mark Batty,
Alastair F. Donaldson, and John Wickerson (POPL'16). The original 
is available online: http://multicore.doc.ic.ac.uk/overhauling/
 *)

"OpenCL"

include "opencl_base.cat"

let s1 = hb
let s2 = fsb?; co; sbf?
let s3 = rf^-1; [SC]; co
let s4 = rf^-1; (hb & loc); [W]
let s44 = ([R] \ (rf^-1; rf)); fr
let s5 = fsb; fr
let s6 = fr; sbf
let s7 = fsb; fr; sbf
let sall = s1|s2|s3|s4|s44|s5|s6|s7
let sp = (sall & (SC*SC)) \ id

let s_cond1 = uni \ (uni; ([SC \ (SY & FGA)]); uni)
let s_cond2 = uni \ (uni; ([SC \ (DV \ FGA)]); uni)
			  
acyclic sp & (s_cond1 | s_cond2) as Spartial
